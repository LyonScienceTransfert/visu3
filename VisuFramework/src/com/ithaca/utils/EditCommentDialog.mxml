<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark" 
	xmlns:mx="library://ns.adobe.com/flex/mx"
	creationComplete="titlewindow1_creationCompleteHandler(event)"
	close="PopUpManager.removePopUp(this);" >
	<s:title>{fxgt.gettext("Votre commentaire")}</s:title>
	<s:states>
		<s:State name="normal"/>
	</s:states>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	<fx:Script>
	<![CDATA[
		import com.ithaca.traces.Obsel;
		import com.ithaca.traces.model.TraceModel;
		import com.ithaca.visu.events.ObselEvent;
		import com.ithaca.visu.model.User;
		import com.ithaca.visu.ui.utils.IconEnum;
		
		import flash.events.KeyboardEvent;
		
		import gnu.as3.gettext.FxGettext;
		import gnu.as3.gettext._FxGettext;
		
		import mx.controls.Alert;
		import mx.events.CloseEvent;
		import mx.events.FlexEvent;
		import mx.managers.PopUpManager;
		
		[Bindable]
		private var fxgt:_FxGettext;
		
		private var _user:User;
		private var userChange:Boolean;
		private var _obsel:Obsel;
		private var obselChange:Boolean;
		
		public var confirm : Boolean = false;	
		public var deleting : Boolean = false;
		public var textComment:String;
		private var _currentObselSkin:Object = null;
		
		////////////////////
		////// setters/getters
		////////////////////
		public function set user(value:User):void
		{
			_user = value;
			userChange = true;
			invalidateProperties();
		}
		public function get user():User
		{
			return _user;
		}
		public function set obsel(value:Obsel):void
		{
			_obsel = value;
			obselChange = true;
			invalidateProperties();
		}
		public function get obsel():Obsel
		{
			return _obsel;
		}		
			
		public function set currentObselSkin(value:Object):void
		{
			_currentObselSkin = value;
		}
		public function get currentObselSkin():Object
		{
			return _currentObselSkin;
		}		
		
		
		
		//_____________________________________________________________________
		//
		// Overriden Methods
		//
		//_____________________________________________________________________
		
		override protected function commitProperties():void
		{
			super.commitProperties();	
			if(userChange)
			{
				userChange = false;
				if(imageUser != null)
				{
					imageUser.source = user.avatar;
				}
				if(nameUser != null)
				{
					nameUser.text = VisuUtils.getUserLabel(user, true);	
				}
			}
			if(obselChange)
			{
				obselChange = false;
				var text:String = "texte de commentaire ici";
				if(obsel)
				{
					text = obsel.props[TraceModel.TEXT];
					buttonDelete.enabled = true;
				}
				textInfo.text = text;
			}
		}

		//_____________________________________________________________________
		//
		// Listeners
		//
		//_____________________________________________________________________
		
		private function onClickButtonOk(event:MouseEvent):void
		{
			confirm = true;
			if(obsel)
			{
				obsel.props[TraceModel.TEXT] = textInfo.text;
			}else
			{
				textComment = textInfo.text;
			}
				
			PopUpManager.removePopUp(this);
		}
		
		private function onClickButtonCancel(event:MouseEvent):void
		{
			PopUpManager.removePopUp(this);
		}
		private function onClickButtonDelete(event:MouseEvent):void
		{
			Alert.yesLabel = fxgt.gettext("Oui");
			Alert.noLabel = fxgt.gettext("Non");
			Alert.show(fxgt.gettext("Voulez-vous supprimer commentaire ?"),
				fxgt.gettext("Confirmation"), Alert.YES|Alert.NO, null, deleteObselConfirmed); 	
		}
		
		protected function deleteObselConfirmed(event:CloseEvent):void
		{
			if( event.detail == Alert.YES )
			{
				deleting = true;
				PopUpManager.removePopUp(this);
			}
		}

		protected function titlewindow1_creationCompleteHandler(event:FlexEvent):void
		{
			fxgt = FxGettext;
			textInfo.setFocus();
			textInfo.selectAll();
		}

	]]>
	</fx:Script>
	<s:VGroup width="100%" height="100%">
		<s:HGroup width="100%" paddingLeft="10" paddingTop="5" verticalAlign="middle" verticalCenter="0">
			<mx:Image  source="{IconEnum.getIconByTypeObsel(TraceModel.SET_TEXT_COMMENT)}"/>
			<s:Label fontWeight="bold">
				<s:text>Votre commentaire pour : </s:text>
			</s:Label>				
		</s:HGroup>
		<s:HGroup width="100%" paddingLeft="10" verticalAlign="middle">
			<mx:Image id="imageUser" width="50" height="50"/>
			<s:Label  id="nameUser" text="Peta Darvin"/>
		</s:HGroup>
		<s:HGroup width="100%" paddingLeft="10" paddingRight="10">
			<s:TextArea  id="textInfo" width="100%" heightInLines="2"/>
		</s:HGroup>
		<s:HGroup width="100%" horizontalAlign="right" paddingRight="10" paddingBottom="5" paddingLeft="10">
			<s:Button id="buttonDelete" click="onClickButtonDelete(event)" enabled="false">
				<s:label>Supprimer</s:label>
			</s:Button>
			<s:Button id="buttonOk" click="onClickButtonOk(event)">
				<s:label>Valider</s:label>
			</s:Button>
			<s:Button id="buttonCancel" click="onClickButtonCancel(event)">
				<s:label>Annuler</s:label>
			</s:Button>
		</s:HGroup>
	</s:VGroup>
</s:TitleWindow>

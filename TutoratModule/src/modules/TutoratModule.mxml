<?xml version="1.0" encoding="utf-8"?>
<modules:VisuModuleBase xmlns:fx="http://ns.adobe.com/mxml/2009" 
						xmlns:s="library://ns.adobe.com/flex/spark" 
						xmlns:mx="library://ns.adobe.com/flex/mx" 
						xmlns:modules="com.ithaca.visu.modules.*" 
						xmlns:maps="maps.*"
						xmlns:mate="http://mate.asfusion.com/"
						configure="visumodulebase1_configureHandler(event)"
						creationComplete="visumodulebase1_creationCompleteHandler(event)"
						width="100%" height="100%" xmlns:controls="com.lyon2.controls.*" 
						currentState="BlankState" 
						xmlns:sessions="com.ithaca.visu.controls.sessions.*" xmlns:local="*" xmlns:timeline="com.ithaca.visu.controls.timeline.*">
	<fx:Script>
		<![CDATA[
			import com.ithaca.traces.Obsel;
			import com.ithaca.traces.model.TraceModel;
			import com.ithaca.traces.view.ObselLine;
			import com.ithaca.traces.view.ObselMarker;
			import com.ithaca.traces.view.ObselSessionOut;
			import com.ithaca.traces.view.ObselStich;
			import com.ithaca.traces.view.ObselTime;
			import com.ithaca.traces.view.skins.ObselMarkerSkin;
			import com.ithaca.utils.UtilFunction;
			import com.ithaca.visu.controls.globalNavigation.event.ApplicationMenuEvent;
			import com.ithaca.visu.controls.sessions.ActivityDetail;
			import com.ithaca.visu.controls.sessions.ActivityDetailB;
			import com.ithaca.visu.controls.sessions.SharedElementChat;
			import com.ithaca.visu.controls.timeline.TraceLine;
			import com.ithaca.visu.controls.timeline.TraceLineB;
			import com.ithaca.visu.events.ActivityElementEvent;
			import com.ithaca.visu.events.InitMapEvent;
			import com.ithaca.visu.events.MessageEvent;
			import com.ithaca.visu.events.ObselEvent;
			import com.ithaca.visu.events.SessionEvent;
			import com.ithaca.visu.events.SessionSharedEvent;
			import com.ithaca.visu.events.TraceLineEvent;
			import com.ithaca.visu.events.VisuActivityEvent;
			import com.ithaca.visu.events.VisuModuleEvent;
			import com.ithaca.visu.interfaces.IDocument;
			import com.ithaca.visu.model.Activity;
			import com.ithaca.visu.model.ActivityElement;
			import com.ithaca.visu.model.ActivityElementType;
			import com.ithaca.visu.model.DocumentActionType;
			import com.ithaca.visu.model.Model;
			import com.ithaca.visu.model.Session;
			import com.ithaca.visu.model.User;
			import com.ithaca.visu.model.vo.ObselVO;
			import com.ithaca.visu.ui.utils.ConnectionStatus;
			import com.ithaca.visu.ui.utils.IconEnum;
			import com.ithaca.visu.ui.utils.RoleEnum;
			import com.ithaca.visu.ui.utils.SessionStatusEnum;
			import com.ithaca.visu.ui.utils.SharedSatusEnum;
			import com.lyon2.controls.ImageDocument;
			import com.lyon2.controls.YoutubePlayer;
			import com.youtube.player.events.PlayerEvent;
			import com.youtube.player.events.PlayerSharedEvent;
			
			import gnu.as3.gettext.FxGettext;
			import gnu.as3.gettext._FxGettext;
			
			import mx.collections.ArrayCollection;
			import mx.collections.ArrayList;
			import mx.controls.Alert;
			import mx.controls.Image;
			import mx.core.IUIComponent;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.events.ResizeEvent;
			
			import spark.components.Button;
			import spark.components.Label;
			
			private var currentSession:Session = null;
			private var _stopTimeRecording:Number = 0;
			private var DELTA_STICH:int = 20;
			
			private var _currentActivityElement:ActivityElement = null;
			
			private var listObjectMedia:ArrayCollection = new ArrayCollection();
			private var _listObjectMediaPlaying:ArrayCollection = new ArrayCollection();
			private var _listTempViewObselSessionOut:ArrayCollection = new ArrayCollection();
			[Bindable]
			public var listActivities:ArrayCollection;
			
			[Bindable]
			private var fxgt:_FxGettext;
					
			[Bindable]
			[Embed("images/fichierVisu1.png")]
			private var fichierIconVisu1:Class;
			
			[Bindable]
			private var timer:Timer;
			
			private var testTimeLine:TraceLineB;
			
			private var testCursor:ObselLine;
			
			protected function visumodulebase1_configureHandler(event:VisuModuleEvent):void
			{
				if((event.currentTarget as TutoratModule).parameters != null  || Model.getInstance().getCurrentSession() != null )
				{
					var session:Session = (event.currentTarget as TutoratModule).parameters as Session;
					if(session == null)
					{
						session = Model.getInstance().getCurrentSession(); 
					}
					this.currentSession = session;
					// set current session in the model for checking if new use join this session  
					Model.getInstance().setCurrentSession(session);
					// update button "salon synchrone"
					Model.getInstance().setEnabledButtonSalonSynchrone(true);
					// FIXME many instance of TutoratModul
					Model.getInstance().setCurrentTutoratModule(this);
					// init list traceLines
					Model.getInstance().initListTraceLine();
					
					var initMapEvent:InitMapEvent = new InitMapEvent(InitMapEvent.INIT_MAP_TUTORAT);
					this.dispatchEvent(initMapEvent);
					// set current state
					if(Model.getInstance().getLoggedUser().role > RoleEnum.STUDENT)
					{
						this.setCurrentState("MainState");				
					}else
					{
						this.setCurrentState("StudentState");		
					}		
				}
				else{
					this.setCurrentState("BlankState");
				}
			}
			
			protected function visumodulebase1_creationCompleteHandler(event:FlexEvent):void
			{
				if((this.currentState != "BlankState") && (Model.getInstance().getCurrentSession() != null) ){
					
					fxgt = FxGettext;
					// set group setMarker enabled for all users
					this.setEnabledGroupSetMarker(false);
					if(buttonStartRecording != null){
						buttonStartRecording.label = fxgt.gettext("Démarrer la séance");
						this.buttonCloseSession.enabled = true;
						if (this.currentSession.statusSession == SessionStatusEnum.SESSION_OPEN || this.currentSession.statusSession == SessionStatusEnum.SESSION_RECORDING )
						{
							this.buttonCloseSession.enabled = false;
						}
						// checking if used has trace
						var obj:Object = Model.getInstance().getTraceLineByUserId(Model.getInstance().getLoggedUser().id_user);
						if(obj != null)
						{
							this.buttonCloseSession.enabled = true;
						}
						// set buttons startActivity enabled = false
						this.sessionPlan.setSessionStatus(VisuVisio.STATUS_NONE);
					}
					
					Model.getInstance().updateStatusLoggedUser(ConnectionStatus.CONNECTED);
					var visuActivityEvent:VisuActivityEvent = new VisuActivityEvent(VisuActivityEvent.LOAD_LIST_ACTIVITY);
					visuActivityEvent.sessionId = Model.getInstance().getCurrentSession().id_session;					
					dispatchEvent(visuActivityEvent);
					
					this.visio.connection = Model.getInstance().getNetConnection();
					this.visio.streamID = Model.getInstance().getUserIdClient();
					this.visio.addLocalDevice();
					if(panelPlanSession != null)
					{
						panelPlanSession.title = panelPlanSession.title +' : '+ Model.getInstance().getCurrentSession().theme;
					}
					// add streams of other users from this session
					//var listSessionUsers:ArrayCollection = Model.getInstance().getCurrentSession().participants;
					var listIdClient:Array = Model.getInstance().getListIdClient(Model.getInstance().getCurrentSession().id_session);
					if(listIdClient != null)
					{
						// FIXME : if session end recording and for logged user status = 0 , this status by default add to all streams, it's no good!!!
						this.visio.addVideoStreams(listIdClient);	
					}
					// removing module from the stage
					this.addEventListener(Event.REMOVED_FROM_STAGE, onRemovedModuleFromStage);
				}
			}
			
			/**
			 * Removing all streams and local devices
			 */
			private function onRemovedModuleFromStage(event:Event):void{
				if(this.visio != null)
				{
					this.visio.removeAllStreams();
					this.visio.removeLocalDevice();				
				}
				Model.getInstance().setCurrentTutoratModule(null);
				this._listTempViewObselSessionOut = new ArrayCollection();
				// remove all images from panelMediaSession
				if(this.panelMediaSession != null)
				{ 	
					var player:YoutubePlayer;
					for each(player in this.listObjectMedia)
					{
						var isPlaying:Boolean = player.isPlaying();
						if(isPlaying)
						{
							// shared info : student walk out from session  
							var sessionId:int = Model.getInstance().getCurrentSession().id_session;
							var listUsersIds:Array = Model.getInstance().getListUsersIdByRecordingSession(sessionId, RoleEnum.STUDENT, true);
							var sessionSharedEvent:SessionSharedEvent = new SessionSharedEvent(SessionSharedEvent.SEND_SHARED_INFO);
							sessionSharedEvent.typeInfo = DocumentActionType.valueOf(DocumentActionType.ACTION_DOCUMENT_VIDEO);
							// this information will add to the obsel
							sessionSharedEvent.info = event.target.toolTip;
							sessionSharedEvent.listUsers = listUsersIds;
							sessionSharedEvent.senderUserId = 	player.getSenderId();
							sessionSharedEvent.info = player.toolTip;
							// set status record for add obsel sharedPlayerAction when session on Paused
							sessionSharedEvent.status = VisuVisio.STATUS_RECORD;	
							sessionSharedEvent.url = player.url;
							sessionSharedEvent.idDocument = player.getIdDocument();
							sessionSharedEvent.currentTimeVideoPlayer = player.getCurrentTime();
							sessionSharedEvent.action = TraceModel.STOP_VIDEO;
							dispatchEvent(sessionSharedEvent);	
							// set pause video
							player.pauseVideo();
						}
					}
					this.panelMediaSession.removeAllElements();
				}
			}
			
			/**
			 * New user join to the session
			 */
			private function onNewUserJoinSession(event:SessionEvent):void{	
				// check if user in current session
				var currentSession:Session = Model.getInstance().getCurrentSession();
				if(currentSession != null)
				{
					if(currentSession.id_session == event.sessionId)
					{
						var module:TutoratModule = Model.getInstance().getCurrentTutoratModule() as TutoratModule;
						if(module == this && module.visio != null)
						{
							module.visio.addVideoStream(event.userIdClient);
							// FIXME: VisuVisio.STATUS_RECORD not the same value ConnectionStatus.RECORDING
							if(event.status == ConnectionStatus.RECORDING)
							{
								module.visio.status = VisuVisio.STATUS_RECORD;
							}
							// status session
							var statusSession:int = currentSession.statusSession;
							if(statusSession != SessionStatusEnum.SESSION_OPEN )
							{								
								// update sharedIconSource
								var user:User = Model.getInstance().getUserByUserId(event.userId);
								if(user != null)
								{
									var role:int = user.role;
									if(role < RoleEnum.TUTEUR)
									{
										// get traceLineView
										var nbrTraceLine:int = this.traceLineGroup.numElements;
										for(var nTraceLine:int = 0; nTraceLine < nbrTraceLine; nTraceLine++)
										{
											var traceLineView:TraceLineB = this.traceLineGroup.getElementAt(nTraceLine) as TraceLineB;
											if( traceLineView.idUserTraceLine == event.userId)
											{
												traceLineView.sharedIconMarkerCode = SharedSatusEnum.SHARED_OK_OTHER_TO_THIS;
											}
										}	
									}
								}
								
								if(statusSession == SessionStatusEnum.SESSION_PAUSE)
								{
									var traceLine:Object = Model.getInstance().getTraceLineByUserId(event.userId)
									// TODO gestion currentTime
									Model.getInstance().addViewObselSessionOut(new Date().time , event.userId);
									
								}else
								{
									// remove viwSessionOut
									var isUserSecondTimeEnterSession:Boolean = Model.getInstance().removeViewObselSessionOut(event.userId);
									// will false when user enter first time in the session
									if(!isUserSecondTimeEnterSession)
									{
										var viewObsel:ObselSessionOut = new ObselSessionOut();
										viewObsel.setOwner(event.userId);
										viewObsel.setBegin(currentSession.date_start_recording.time);
										// TODO gestion currentTime
										viewObsel.setEnd(new Date().time);
										// add viewObsel in temp list before adding to traceLine, traveLine don't ready now
										this._listTempViewObselSessionOut.addItem(viewObsel);
									}																
								}
							}	
						}
					}							
				}
			}
			
			/**
			 * Old user walk out from the session
			 */
			private function onOldUserOutSession(event:SessionEvent):void
			{
				var currentModule:TutoratModule = Model.getInstance().getCurrentTutoratModule() as TutoratModule;
				if(this == currentModule)
				{
					// streamId of the user 
					var streamId:String = event.userIdClient;
					// remove video of the user
					currentModule.visio.removeVideoStream(streamId);
					// add message in the chat for not logged users 
					// TODO icon for this event
					if(event.userId != Model.getInstance().getLoggedUser().id_user)
					{
						this.initChatPanel(event.userId, fxgt.gettext("a quitté la séance"),"");	
						// add viewObsel "SessionOut"
						// FIXME gestion time
						Model.getInstance().addViewObselSessionOut(new Date().time,event.userId)
					}
				}	
			}

			private function createSessionTimeLine(currentSession:Session):void
			{
				var durationSession:int = currentSession.duration_session;
				this.sessionTimeLine.sessionTimeLineLayout.durationSession = durationSession;
				this.sessionTimeLine.sessionTimeLineLayout.startTime = 0;
/* 				// TODO calcul number of labels
				var nbrLabelTime:int = 6;
				var deltaTime:int = durationSession/nbrLabelTime;
				for(var nPart:int = 0 ; nPart < nbrLabelTime; nPart++)
				{
					var beginTimeObsel:Number = nPart*deltaTime;
					// in seconds
					var labelTextSec:String = (beginTimeObsel/1000).toString();
					var labelTextMin:String = Math.round((beginTimeObsel/1000)/60).toString();
					var obselTime:ObselTime = new ObselTime()
					obselTime.setBegin(beginTimeObsel);
					obselTime.setEnd(beginTimeObsel);
					obselTime.text = labelTextMin+" min.";
					this.sessionTimeLine.sessionTimeLineLoggedUser.addElement(obselTime);
				} */
				var widthGroup:Number = this.sessionTimeLine.sessionTimeLineLoggedUser.width;
				var nbrLabelTime:int = widthGroup/DELTA_STICH;
				nbrLabelTime = 50;
				var deltaTime:int = durationSession/nbrLabelTime;
				var every:int = 6;
				var count:int = 1;
				for(var nPart:int = 0 ; nPart < nbrLabelTime; nPart++)
				{
					var beginTimeObsel:Number = nPart*deltaTime;
					// in seconds
					
					var obselTime:ObselStich = new ObselStich()
					obselTime.setBegin(beginTimeObsel);
					obselTime.setEnd(beginTimeObsel);
					obselTime.height = 10;
					if(count == every)
					{
						obselTime.height = 20;
						count = 1;
						var minuts:int = (beginTimeObsel/1000)/60;
						var minutsString:String = minuts.toString();
						if(minuts<10){
							minutsString = "0"+minutsString; 
						}
						var seconds:int = (beginTimeObsel/1000)-minuts*60;
						var secondsString:String = seconds.toString();
						if(seconds < 10)
						{
							secondsString = "0"+secondsString;
						}
						obselTime.setLabel(minutsString+":"+secondsString);
					}
					count++;
					this.sessionTimeLine.sessionTimeLineLoggedUser.addElement(obselTime);
				}
				// creation slider session startTime
				var startSession:Number = currentSession.date_start_recording.getTime();
				this.startTimeSessionSlider.minimum = startSession;
				this.startTimeSessionSlider.maximum = startSession + durationSession - 60000;
				this.startTimeSessionSlider.value = startSession;
				this.startTimeSessionSlider.dataTipFormatFunction = formatStartTimeSlider;
				this.startTimeSessionSlider.toolTip = "Temps du début de la séance";			
				// creation slider session duration
				this.durationSessionSlider.minimum = 60000;
				this.durationSessionSlider.maximum = durationSession*2;
				this.durationSessionSlider.value  = durationSession;		
				this.durationSessionSlider.dataTipFormatFunction = formatDurationSlider;
				this.durationSessionSlider.toolTip = "Durée de la séance"
			}
			

			/* 			
			private function formatDurationSlider(value:Number):Object { 
				var duration:int = value/60000;
				var result:String = duration.toString() + " min.";
				return result;
			}
			
			private function formatStartTimeSlider(value:Number):Object { 
				var duration:int = (value - this.currentSession.date_start_recording.getTime())/60000;
				var result:String = duration.toString() + " min.";
				return result;
			} */ 
			/**
			 * format text slider duration of the session
			 */
			private function formatDurationSlider(value:Number):Object { 
				var duration:int = value/60000;
				var percent:int = (value*100/durationSessionSlider.maximum);
				var result:String = percent.toString() + " %";
				return result;
			}
			
			/**
			 * format text slider start session
			 */
			private function formatStartTimeSlider(value:Number):Object { 
				var duration:int = (value - startTimeSessionSlider.minimum)/60000;
				var result:String = duration.toString() + " min.";
				return result;
			}
			
			/**
			 * Update status on recording mode
			 */
			private function onStartRecordingSession(event:SessionEvent):void
			{
				if(this.visio != null)
				{
					this.visio.status = VisuVisio.STATUS_RECORD;
					// set group setMarker enabled for all users
					this.setEnabledGroupSetMarker(true);
					// update label button on view tutorat
					if(buttonStartRecording != null){			
						buttonStartRecording.label = fxgt.gettext("Suspendre la séance");	
						buttonCloseSession.enabled = false;
						// set buttons startActivity enabled = true
						this.sessionPlan.setSessionStatus(VisuVisio.STATUS_RECORD);							
					}
					// current session
					var session:Session = Model.getInstance().getCurrentSession();
					// remove all user paused presentred in the session
					Model.getInstance().removeObselSessionOutCurrentUser(session.id_session);	
				}
			}
			
			private function startTimer():void
			{
				if(!timer)
				{
					timer = new Timer(1000,0);
					timer.addEventListener(TimerEvent.TIMER, updateTime);
				}
				timer.start();
				var currentsession:Session = Model.getInstance().getCurrentSession();
				if(currentsession == null)
				{
					Alert.show("Bug report", "You have a problem with the current session");
				}else
				{
					// create SessionTimeLine
					this.createSessionTimeLine(currentsession);
				}
			}
			
			private function updateTime(event:TimerEvent):void
			{
				var currentTimeServeur:Number = Model.getInstance().getTimeServeur();
				var beginTime:Number =  currentTimeServeur - this.currentSession.date_start_recording.time;
				this.testCursor.setBegin(beginTime);
				this.testCursor.setEnd(beginTime);
				this.sessionTimeLine.labelCurrentTimeSession.text = updateLabelTimer(beginTime);
				this.sessionTimeLine.sessionTimeLineLoggedUser.invalidateDisplayList();
				// update obsels "SessionOut"
				Model.getInstance().setTimeViewObselSessionOut(beginTime + this.currentSession.date_start_recording.time);
				var nbrTraceLine:int = this.traceLineGroup.numElements;
				for(var nTraceLine:int = 0; nTraceLine < nbrTraceLine; nTraceLine++)
				{
					var traceLine:TraceLineB = this.traceLineGroup.getElementAt(nTraceLine) as TraceLineB;
					traceLine.updateObsel();
				}	 			
			}
			
			private function updateLabelTimer(value:Number):String
			{
				var totalSecond:int = value/1000;
				var minNumber:int = new int(totalSecond/60);
				var hourNumber:int = new int(totalSecond/3600);
				var secString:String="";
				var minString:String="";
				var hourString:String="";
				var secons:Number = totalSecond - minNumber*60;
				secString = secons.toString();
				if(secons < 10)
				{
					secString = "0"+secString;
				}
				var min:Number = minNumber - hourNumber*60;
				minString = min.toString();
				if(min < 10)
				{
					minString = "0" + minString;
				}
				hourString = hourNumber.toString();
				if(hourNumber < 10)
				{
					hourString = "0"+hourString;	
				}
				return  hourString+":"+minString+":"+secString;
				
			}

			/**
			 * Update status on stop recording session
			 */
			private function onStopRecordingSession(event:SessionEvent):void
			{
				var currentModule:TutoratModule = Model.getInstance().getCurrentTutoratModule() as TutoratModule;
				if(this == currentModule)
				{
					this.visio.status = VisuVisio.STATUS_NONE;
					// set group setMarker enabled for all users
					this.setEnabledGroupSetMarker(false);
					// update label button on view tutorat
					if(buttonStartRecording != null){
						buttonStartRecording.label = fxgt.gettext("Démarrer la séance");
						buttonCloseSession.enabled = true;
						// TODO make that obsel "OutSession" will getting wight
						this._stopTimeRecording = event.timeStartStop;
						//this.timer.stop();
						// set buttons startActivity enabled = false
						this.sessionPlan.setSessionStatus(VisuVisio.STATUS_NONE);
					}
					// current session
					var session:Session = Model.getInstance().getCurrentSession()
					// add obsel "SessionOut" for user in the session
					Model.getInstance().setObselSessionOutForCurrentUser(session.id_session);
					
				}
			}
			
			/**
			 * Update list activity
			 */ 
			// FIXME : 	Echec de la contrainte de type, look at TutoratManageronLoadListActivityElement
			//	public function updateView(event:VisuActivityEvent):void
			public function updateView(listActivity:ArrayCollection):void
			{
				
			}

			
			private function onUpdateViewChatPanelSession(event:SessionSharedEvent):void
			{
				var currentModule:TutoratModule = Model.getInstance().getCurrentTutoratModule() as TutoratModule;
				if(this == currentModule)
				{
					var idDocument:Number=0;
					// obselVO will be null only if session paused or not started
					var obselVO:ObselVO = event.obselVO as ObselVO;
					if( obselVO != null)
					{
						// set obsel to the model
						var obsel:Obsel = Obsel.fromRDF(obselVO.rdf);
						//this.addObsel(obsel);
						Model.getInstance().addObsel(obsel);
						// get id document
						idDocument = obsel.props[TraceModel.ID_DOCUMENT];
					}
					// update the buttons on planSession, view tutorat
					if(this.sessionPlan != null && obselVO != null && obselVO.type == TraceModel.ACTIVITY_START)
					{
						var obselStartSession:Obsel = Obsel.fromRDF(obselVO.rdf);
						var aa:String = obselStartSession.props[TraceModel.ACTIVITY_ID]; 
						var activityId:int = new int(aa);
						
						this.sessionPlan.setCurrentActivityId(activityId);
					}
					
					var pathAvatar:String = "";
					var name:String = "";
					var sourceImageInfo:* = null;
					var senderUser:User = Model.getInstance().getUserByUserId(event.senderUserId);
					if(senderUser != null)
					{
						pathAvatar = senderUser.avatar;
						name = senderUser.firstname + " "+ senderUser.lastname;						
					}
					
					if(((event.typeInfo as int) != ActivityElementType.valueOf(ActivityElementType.MARKER)) && ((event.typeInfo as int) != DocumentActionType.valueOf(DocumentActionType.ACTION_DOCUMENT_VIDEO)))
					{
						switch (event.typeInfo as int) 
						{
							case ActivityElementType.valueOf(ActivityElementType.VIDEO) :
								sourceImageInfo = IconEnum.getIconByTypeObsel(TraceModel.SEND_DOCUMENT);
								// set players on paused than add new player and than start playing(without shared info)
								if(this.listObjectMedia.length > 0)
								{
									var playerOnStage:YoutubePlayer;
									for each(playerOnStage in this.listObjectMedia)
									{
										var isPlaying:Boolean = playerOnStage.isPlaying();
										if(isPlaying)
										{
											playerOnStage.pauseVideo();
											this._listObjectMediaPlaying.addItem(playerOnStage);
										}
									}
								}
								var player:YoutubePlayer = new YoutubePlayer();
								// set id of the sender user 
								player.setSenderId(event.senderUserId);
								player.setIdDocument(idDocument);
								player.url = event.url;
								player.percentWidth = 100;
								player.height = 225;
								player.toolTip = event.info;
								player.addEventListener(PlayerEvent.READY, loadComplete);
								player.addEventListener(FlexEvent.UPDATE_COMPLETE, onPlayerUpdateComplete);
								if(this.panelMediaSession != null)
								{
									this.panelMediaSession.addElementAt(player, 0);
								}
								break;
							case ActivityElementType.valueOf(ActivityElementType.IMAGE) :
								sourceImageInfo = event.url;
								var image:ImageDocument = new ImageDocument();
								// set id of the sender user
								image.setSenderId(event.senderUserId);
								image.setIdDocument(idDocument);
								image.load(event.url);
								image.toolTip = event.info;
								image.percentWidth = 100;	
								image.addEventListener(Event.ADDED_TO_STAGE, loadComplete);
								if(this.panelMediaSession != null)
								{
									this.panelMediaSession.addElementAt(image, 0);
								}
								break;
							case ActivityElementType.valueOf(ActivityElementType.MESSAGE) :
								sourceImageInfo = IconEnum.getIconByTypeObsel(TraceModel.SEND_CHAT_MESSAGE);
								break;
							case ActivityElementType.valueOf(ActivityElementType.KEYWORD) :
								sourceImageInfo = IconEnum.getIconByTypeObsel(TraceModel.SEND_KEYWORD);; 
								break;
							case ActivityElementType.valueOf(ActivityElementType.STATEMENT) :
								sourceImageInfo = IconEnum.getIconByTypeObsel(TraceModel.SEND_INSTRUCTIONS);
								break;
							case ActivityElementType.valueOf(ActivityElementType.READ_DOCUMENT_IMAGE) :
								sourceImageInfo = fichierIconVisu1;
								break;
							case ActivityElementType.valueOf(ActivityElementType.READ_DOCUMENT_VIDEO) :
								sourceImageInfo = IconEnum.getIconByTypeObsel(TraceModel.SEND_DOCUMENT);
								break;
							case ActivityElementType.valueOf(ActivityElementType.START_ACTIVITY) :
								sourceImageInfo = fichierIconVisu1;
								break;
						}
						
						var sharedElementChat:SharedElementChat = new SharedElementChat();
						// default color of component SharedElementChatSkin 
						// only for chat messages sended before start session(hasn't info about users)
						var userColor:uint = sharedElementChat.defaultColorFullColorGradientExit;
						var traceLine:Object = Model.getInstance().getTraceLineByUserId(event.senderUserId);
						if(traceLine != null)
						{
							userColor = traceLine.userColor;
						}
						sharedElementChat.setElementChat(pathAvatar,name,event.info, sourceImageInfo, userColor);
						sharedElementChat.statVciel =  Model.getInstance().checkServeurVisuVciel();
						this.panelChatSession.addEventListener(FlexEvent.UPDATE_COMPLETE, onUpdateChatSession);
						this.panelChatSession.addElement(sharedElementChat);
					}
				}
			}
			
			private function onSharedActionPlayer(event:PlayerSharedEvent):void
			{
				var player:YoutubePlayer = event.target as YoutubePlayer;
				var action:String = event.action;
				var currentTime:Number = event.currentTime;
				var idDocument:Number = player.getIdDocument();
				this.panelPlanSession.title = action+ " "+ int(currentTime).toString();
				var sessionId:int = Model.getInstance().getCurrentSession().id_session;
				var listUsersIds:Array = Model.getInstance().getListUsersIdByRecordingSession(sessionId, RoleEnum.STUDENT, true);
				var sessionSharedEvent:SessionSharedEvent = new SessionSharedEvent(SessionSharedEvent.SEND_SHARED_INFO);
				sessionSharedEvent.typeInfo = DocumentActionType.valueOf(DocumentActionType.ACTION_DOCUMENT_VIDEO);
				// this information will add to the obsel
				sessionSharedEvent.info = event.target.toolTip;
				sessionSharedEvent.listUsers = listUsersIds;
				sessionSharedEvent.senderUserId = 	player.getSenderId();
				// set status record for add obsel sharedPlayerAction when session on Paused
				sessionSharedEvent.status = VisuVisio.STATUS_RECORD;	
				sessionSharedEvent.url = player.url;
				sessionSharedEvent.idDocument = idDocument;
				sessionSharedEvent.currentTimeVideoPlayer = currentTime;
				sessionSharedEvent.action = action;
				dispatchEvent(sessionSharedEvent);			
			}

			public function loadComplete(event:Event):void
			{
				var player:YoutubePlayer = event.target as YoutubePlayer;
				if(player != null){
					trace('loadComplete');
					resizeMe(event.target);
					this.listObjectMedia.addItem(player);
					player.addEventListener(PlayerSharedEvent.SHARED, onSharedActionPlayer);
				} 
				// send message to serveur(all users of current session exclus me/loggedUser) that user can see recived Document
 				var url:String= "";
				var typeInfo:int=-1;
				if(event.target.className == "ImageDocument")
				{
					url = event.target.source as String;
					typeInfo = ActivityElementType.valueOf(ActivityElementType.READ_DOCUMENT_IMAGE);
				}else{
					url = event.target.url as String;
					typeInfo = ActivityElementType.valueOf(ActivityElementType.READ_DOCUMENT_VIDEO);
				}
				var senderUserId:int = (event.target as IDocument).getSenderId();
				var idDocument:Number = (event.target as IDocument).getIdDocument();
				// get list users id without logged user
				// logged user can't partage info with himself 
				var listUsersIds:Array = Model.getInstance().getListUsersIdByRecordingSession(this.currentSession.id_session, RoleEnum.STUDENT, true);
				var sessionSharedEvent:SessionSharedEvent = new SessionSharedEvent(SessionSharedEvent.SEND_SHARED_INFO);
				sessionSharedEvent.typeInfo = typeInfo;
				// this information will add to the obsel
				sessionSharedEvent.info = event.target.toolTip;
				sessionSharedEvent.listUsers = listUsersIds;
				sessionSharedEvent.senderUserId = senderUserId;
				sessionSharedEvent.status = this.visio.status;	
				sessionSharedEvent.url = url;
				sessionSharedEvent.idDocument = idDocument;
				dispatchEvent(sessionSharedEvent);			
			}
			
			public function resizeMe(o:Object):void
			{
				var ratio:Number;
				if(o is Image)
				{
					ratio= o.contentWidth/o.contentHeight;
				}
				if( o is YoutubePlayer )
				{
					ratio = 4/3;
				}
				o.height =  o.width / ratio ;			
			}
			
			private function onPlayerUpdateComplete(event:FlexEvent):void
			{
				var player:YoutubePlayer = event.target as YoutubePlayer;
				player.removeEventListener(FlexEvent.UPDATE_COMPLETE, onPlayerUpdateComplete);
				if (scrollerPanelMediaSession != null){
					scrollerPanelMediaSession.viewport.verticalScrollPosition = 0;
				}
				this.setPlyingAllPlayerWasPaused();
			}
			
			private function setPlyingAllPlayerWasPaused():void
			{
				if(this._listObjectMediaPlaying.length > 0)
				{
					var playerPaused:YoutubePlayer;
					for each (playerPaused in this._listObjectMediaPlaying)
					{
						playerPaused.playVideo();
					}
					this._listObjectMediaPlaying.removeAll();
				}
			}
			protected function onUpdateChatSession(event:FlexEvent):void{
				// update scroll possition
				scrollerPanelChatSession.viewport.verticalScrollPosition = scrollerPanelChatSession.viewport.contentHeight - scrollerPanelChatSession.height;
			}
			
			private function doShareActivityElement(value:ActivityElement):void
			{
				var sessionId:int = Model.getInstance().getCurrentSession().id_session;
				var listUsersId:Array = Model.getInstance().getListUsersIdByRecordingSession(sessionId);
				var sessionSharedEvent:SessionSharedEvent = new SessionSharedEvent(SessionSharedEvent.SEND_SHARED_INFO);
				sessionSharedEvent.typeInfo = ActivityElementType.valueOf(value.type_element);
				sessionSharedEvent.info = value.data;
				sessionSharedEvent.listUsers = listUsersId;
				sessionSharedEvent.status = this.visio.status;	
				sessionSharedEvent.url = value.url_element;
				dispatchEvent(sessionSharedEvent);	
			}
			protected function shareActivityElement(event:ActivityElementEvent):void
			{
				trace("shareActivityElement");
				if( this.visio.status != VisuVisio.STATUS_RECORD)
				{
					Alert.show(fxgt.gettext("Poser un marqueur, une consigne, ou un mot-clé est autorisé uniquement après avoir démarré la séance.") , fxgt.gettext("Confirmation"));
				}else
				{
					// check if activityElement from current activity or activityElement is Keyword
					var activityIdOfChoosenActivityElement:int = event.element.id_activity;
					var currentActivityId:int = this.sessionPlan.getCurrentActivityId();
					if(activityIdOfChoosenActivityElement == currentActivityId)
					{
						// shared activityElement
						this.doShareActivityElement(event.element)
					}else
					{
						_currentActivityElement = event.element;
						Alert.yesLabel = fxgt.gettext("Oui");
						Alert.noLabel = fxgt.gettext("Non");
						Alert.show(fxgt.gettext("Voulez-vous changer d'activité?"),
							fxgt.gettext("Confirmation"), Alert.YES|Alert.NO, null, changeActivityConfirmed); 
					}
				}
			}
			
			private function changeActivityConfirmed(event:CloseEvent):void{
				if( event.detail == Alert.YES )
				{
					var newActivityId:int = this._currentActivityElement.id_activity;
					var activityDetail:ActivityDetailB = this.sessionPlan.getActivityDetailById(newActivityId);
					// for interdir click on the button start the new activity
					activityDetail.startButton.enabled = false;
					this.shareStartActivity(activityDetail.activity);	
					// change activity, shared Activity
					this.doShareActivityElement(this._currentActivityElement);
					// shared activityElement
				}
			}
			
			protected function startActivity(event:VisuActivityEvent):void
			{
				//dispatchEvent 
				event.stopPropagation();
				trace("TODO : start activity "+event.activity.id_activity + ":"+ event.activity.title);
				// set button start activity enabled = false , 
				// for interdir click again before end all of action on server side
				var activityDetail:ActivityDetailB = event.target as ActivityDetailB;
				activityDetail.startButton.enabled = false;
				// shared start activity
				this.shareStartActivity(event.activity);
			}
			
			private function shareStartActivity(value:Activity):void
			{
				var sessionId:int = Model.getInstance().getCurrentSession().id_session;
				var listUsersId:Array = Model.getInstance().getListUsersIdByRecordingSession(sessionId);
				var sessionSharedEvent:SessionSharedEvent = new SessionSharedEvent(SessionSharedEvent.SEND_SHARED_INFO);
				sessionSharedEvent.typeInfo = ActivityElementType.valueOf(ActivityElementType.START_ACTIVITY);
				sessionSharedEvent.info = value.title;
				sessionSharedEvent.listUsers = listUsersId;
				// use url variable for sending id activity
				sessionSharedEvent.url = value.id_activity.toString();
				sessionSharedEvent.status = this.visio.status;	
				sessionSharedEvent.senderUserId = Model.getInstance().getLoggedUser().id_user;	
				dispatchEvent(sessionSharedEvent);
			}
			
			protected function button1_clickHandler(event:MouseEvent):void
			{
				if(this.visio != null)
				{
					if (this.visio.status == VisuVisio.STATUS_RECORD){
						//stop recording	
						// conformation for closing the session
						Alert.yesLabel = fxgt.gettext("Oui");
						Alert.noLabel = fxgt.gettext("Non");
						Alert.show(fxgt.gettext("Voulez-vous arrêter l'enregistrement ? (vous pourrez le reprendre plus tard)"),
						fxgt.gettext("Confirmation"), Alert.YES|Alert.NO, null, stopRecordingConfirmed); 	
					}else{
						
						//start recording
						var eventStartRecording:MessageEvent = new MessageEvent(MessageEvent.START_RECORDING);
						eventStartRecording.sessionId = this.currentSession.id_session;
						dispatchEvent(eventStartRecording);	
					}
				}
			}
			
			/**
			 * Stop recording conformation 
			 */
			protected function stopRecordingConfirmed(event:CloseEvent):void
			{
				var sessionStatus:int = -1;
				if( event.detail == Alert.YES )
				{				
					var sessionId:int = Model.getInstance().getCurrentSession().id_session;
					// shared stop activity
					if(this.sessionPlan != null)
					{
						// check if currentActivity not 0 
						var currentActivityId:int = this.sessionPlan.getCurrentActivityId();
						if(currentActivityId != 0)
						{
							var listUsersId:Array = Model.getInstance().getListUsersIdByRecordingSession(sessionId);
							var sessionSharedEvent:SessionSharedEvent = new SessionSharedEvent(SessionSharedEvent.SEND_SHARED_INFO);
							sessionSharedEvent.typeInfo = ActivityElementType.valueOf(ActivityElementType.STOP_ACTIVITY);
							// info == "void", we have title activity in obsel start activity
							sessionSharedEvent.info = fxgt.gettext("a suspendu la séance.");	
							sessionSharedEvent.listUsers = listUsersId;
							// use url variable for sending id activity
							sessionSharedEvent.url = currentActivityId.toString();
							sessionSharedEvent.status = this.visio.status;	
							sessionSharedEvent.senderUserId = Model.getInstance().getLoggedUser().id_user;	
							dispatchEvent(sessionSharedEvent);
						}
					}
					// stop recording
					sessionStatus =  SessionStatusEnum.SESSION_PAUSE;				
					var eventStopRecording:MessageEvent = new MessageEvent(MessageEvent.STOP_RECORDING);
					eventStopRecording.sessionId = this.currentSession.id_session;
					eventStopRecording.sessionStatus = sessionStatus;
					dispatchEvent(eventStopRecording);		
				}
			}
			
			
			
			/**
			 * Other tuteur close session
			 */
			protected function onCloseSession(event:SessionEvent):void
			{
				if(buttonStartRecording != null)
				{
					buttonStartRecording.enabled = false;
					buttonCloseSession.enabled = false;
					var sessionId:int = event.sessionId;
					var currentSession:Session = Model.getInstance().getCurrentSession();
					if(currentSession != null)
					{
						var currentSessionId:int = currentSession.id_session;
						if(currentSessionId == sessionId)
						{
							Model.getInstance().setCurrentSession(null);
							Model.getInstance().setEnabledButtonSalonSynchrone(false);						
						}
						
					}
				}
			}
			
			protected function updateLabelsModule(event:ApplicationMenuEvent):void
			{
				if(buttonStartRecording != null){
					if(this.visio.status == VisuVisio.STATUS_NONE)
					{				
						buttonStartRecording.label = fxgt.gettext("Démarrer la séance");									
					}else
					{
						buttonStartRecording.label = fxgt.gettext("Suspendre la séance");			
					}
					// update labels the button start activity for translating
					this.sessionPlan.setCurrentActivityId(0);
				}
			}
			
			/**
			 * Checking that sending the message by click on button or click by "enter"
			 */
			protected function onSendMessage(event:*):void
			{
				if(event is MouseEvent){
					this.sendMessage(false);
				}else if (event is KeyboardEvent) 
				{
					if(event.keyCode == Keyboard.ENTER)
					{
						this.sendMessage(true);
					}
				}
			}
			
			/**
			 * Sending message
			 */
			private function sendMessage(cutLastCharMessage:Boolean):void
			{
				// TODO send message to one user by click on his video
				var listUsersId:Array;
				var sessionSharedEvent:SessionSharedEvent = new SessionSharedEvent(SessionSharedEvent.SEND_SHARED_INFO);
				var sessionId:int = Model.getInstance().getCurrentSession().id_session;
				if (this.visio.status == VisuVisio.STATUS_NONE)
				{
					// all connected users
					listUsersId = Model.getInstance().getListUsersIdByConnectedSession(sessionId);
					sessionSharedEvent.status = -1;	
				}else
				{
					// all connected users with status recording
					listUsersId = Model.getInstance().getListUsersIdByRecordingSession(sessionId);
					sessionSharedEvent.status = this.visio.status;
				}
				
				sessionSharedEvent.typeInfo = ActivityElementType.valueOf(ActivityElementType.MESSAGE);
				var message:String = textChatMessage.text;
				if(cutLastCharMessage)
				{
					// remove last character from the message if was click on button "Enter"
					message = textChatMessage.text.slice(0, textChatMessage.text.length-1);
				}
				// check if message empty
				if(UtilFunction.isEmptyMessage(message))
				{
					// don't send empty message
					textChatMessage.text = "";
					return;
				}
				sessionSharedEvent.info = message;
				sessionSharedEvent.listUsers = listUsersId;
				dispatchEvent(sessionSharedEvent);
				// update text
				textChatMessage.text = "";
			}
			
			/**
			 * Checking that sending the marker by click on button or click by "enter"
			 */
			protected function onSetMyMarker(event:*):void
			{
				if(event is MouseEvent){
					this.sendMarker(false);
				}else if (event is KeyboardEvent) 
				{
					if(event.keyCode == Keyboard.ENTER)
					{
						this.sendMarker(true);
					}
				}
			}
			/**
			 *  Sending marker
			 */
			private function sendMarker(cutLastCharMessage:Boolean):void
			{
				var sessionId:int = Model.getInstance().getCurrentSession().id_session;
				var listUsersId:Array;
				if(Model.getInstance().getLoggedUser().role < RoleEnum.TUTEUR)
				{
					// student will shared markers only with Tuter, Responsable, Admin
					listUsersId = Model.getInstance().getListUsersIdByRecordingSession(sessionId, RoleEnum.STUDENT );
				}else
				{
					listUsersId = Model.getInstance().getListUsersIdByRecordingSession(sessionId, RoleEnum.TUTEUR );
				}
				var sessionSharedEvent:SessionSharedEvent = new SessionSharedEvent(SessionSharedEvent.SEND_SHARED_INFO);
				sessionSharedEvent.typeInfo = ActivityElementType.valueOf(ActivityElementType.MARKER);
				var message:String = textChatMarker.text;
				// cut last char if was click on button "Enter"
				if(cutLastCharMessage)
				{
					message = textChatMarker.text.slice(0, textChatMarker.text.length-1);
				}
				// check if message empty
				if(UtilFunction.isEmptyMessage(message))
				{
					message = fxgt.gettext("(vide)");
				}
				sessionSharedEvent.info = message;
				sessionSharedEvent.status = this.visio.status;
				sessionSharedEvent.listUsers = listUsersId;
				dispatchEvent(sessionSharedEvent);
				// update text
				textChatMarker.text = "";
			}
			
 			protected function panelPlanSession_resizeHandler(event:ResizeEvent):void
			{
				event.target.addEventListener(FlexEvent.UPDATE_COMPLETE, resizeChildren);
			} 

 			public function resizeChildren(event:FlexEvent):void
			{
				event.target.removeEventListener(FlexEvent.UPDATE_COMPLETE, resizeChildren);
				var child:IUIComponent; 
				for each(child in  this.listObjectMedia)
				{
					resizeMe(child);
				}
			} 

			protected function buttonCloseSession_clickHandler(event:MouseEvent):void
			{
				if(this.visio != null)
				{
					// conformation for closing the session
					Alert.yesLabel = fxgt.gettext("Oui");
					Alert.noLabel = fxgt.gettext("Non");
					Alert.show(fxgt.gettext("Voulez-vous clôturer la séance ? (il ne sera plus possible d'y rentrer à nouveau)"),
					fxgt.gettext("Confirmation"), Alert.YES|Alert.NO, null, closeSessionConfirmed); 	
				}
			}
			
			/**
			 * Close session conformation 
			 */
			protected function closeSessionConfirmed(event:CloseEvent):void
			{
				var sessionStatus:int = -1;
				if( event.detail == Alert.YES )
				{				
					// stop recording and close session
					sessionStatus =  SessionStatusEnum.SESSION_CLOSE;	
					if(buttonStartRecording != null)
					{
						// can't start session
						buttonStartRecording.enabled = false;
						buttonCloseSession.enabled = false;
					}
					var eventStopRecording:MessageEvent = new MessageEvent(MessageEvent.STOP_RECORDING);
					eventStopRecording.sessionId = this.currentSession.id_session;
					eventStopRecording.sessionStatus = sessionStatus;
					dispatchEvent(eventStopRecording);			
				}
			}
			private function onClickDebugButton(event:Event):void{
				var msg:MessageEvent = new MessageEvent(MessageEvent.GET_SETMARCK);
				//msg.message = "<trace-20100928183134-25>";
				msg.message = "<trace-20101006153326-7>";
				this.dispatchEvent(msg);
				
				
			}
			
			private function addTraceLineOnViewTraceLineGroup(traceLineObj:Object):void
			{
				var userId:int = traceLineObj.userId;
				var traceLine:TraceLineB = new TraceLineB();
				traceLine.percentWidth = 100;
			//	traceLine.height = 30;
				traceLine.left = 0;
				traceLine.right = 0;
				traceLine.sourceImageUserTraceLine = traceLineObj.userAvatar;
				traceLine.nameUserTraceLine = traceLineObj.userName;
				traceLine.colorUserTraceLine = traceLineObj.userColor;
				traceLine.durationSession = this.currentSession.duration_session;
				traceLine.startTimeSession = this.currentSession.date_start_recording.getTime();				
//				traceLine.startTimeSession = Model.getInstance().getBeginTimeSalonSynchrone();					
				traceLine.idUserTraceLine = userId;		
				traceLine.tempList = traceLineObj.listTitleObsels;
				traceLine.listElementTraceline = traceLineObj.listElementTraceLine;
				traceLine.addEventListener(TraceLineEvent.ADD_LIST_OBSEL, onAddListObsel);
				traceLine.addEventListener(TraceLineEvent.REMOVE_LIST_OBSEL, onRemoveListObsel);
				/* traceLine.addEventListener(TraceLineEvent.ADD_TRACE_LINE_ELEMENT, onAddTraceLineElement);
				traceLine.addEventListener(TraceLineEvent.REMOVE_TRACE_LINE_ELEMENT, onRemoveTraceLineElement); */
				traceLine.addEventListener(FlexEvent.CREATION_COMPLETE, onCreationCompletTraceLine);
				// logged user
				var loggedUser:User = Model.getInstance().getLoggedUser();
				var sharedIconMarkerCode:String ="";
				if(userId == loggedUser.id_user)
				{
					sessionTimeLine.sessionTimeLineLayout.startTime = 0;	
					sessionTimeLine.sessionTimeLineLayout.durationSession = this.currentSession.duration_session;				
					traceLine.nameUserTraceLine = fxgt.gettext("Ma trace");
					this.traceLineGroup.addElementAt(traceLine, 0);
					if(loggedUser.role < RoleEnum.TUTEUR)
					{
						// etudiant shared mark by default
						sharedIconMarkerCode = SharedSatusEnum.SHARED_OK_THIS_TO_OTHER;
					}else
					{
						sharedIconMarkerCode = SharedSatusEnum.SHARED_NON_THIS_TO_OTHER;
					}					
				}else
				{
					this.traceLineGroup.addElement(traceLine);
					// FIXMI : if user walk out from session will set shared icon NON_OTHER_TO_THIS
					// TODO : update icon when use join session
					var user:User = Model.getInstance().getUserByUserId(userId);
					if(user != null && user.role < RoleEnum.TUTEUR)
					{
						// etudiant shared mark by default
						sharedIconMarkerCode = SharedSatusEnum.SHARED_OK_OTHER_TO_THIS;
					}else
					{
						sharedIconMarkerCode = SharedSatusEnum.SHARED_NON_OTHER_TO_THIS;
					}	
				}			
				traceLineObj.show = true;
				// set shared icon
				traceLine.sharedIconMarkerCode = sharedIconMarkerCode;	
			}
			
			/**
			 * add list obsel to Model 
			 */	
			private function onAddListObsel(event:TraceLineEvent):void{
				var listObsel:ArrayCollection = event.listObsel;
				var userTraceLineId:int = (event.currentTarget as TraceLineB).idUserTraceLine;
				Model.getInstance().addListObselTitleTraceLine(userTraceLineId, listObsel);
			}
			
			/**
			 * remove list obsel to Model 
			 */	
			private function onRemoveListObsel(event:TraceLineEvent):void{
				var listObsel:ArrayCollection = event.listObsel;
				var userTraceLineId:int = (event.currentTarget as TraceLineB).idUserTraceLine;
				Model.getInstance().removeListObselTitleTraceLine(userTraceLineId, listObsel);
			}
			/**
			 * Update current activity in list activity, update label the button "start" to "en cours"
			 */
			private function onUpdateViewActivity(event:VisuActivityEvent):void{
				var activityId:int = event.activityId;
				if(this.sessionPlan != null)
				{
					this.sessionPlan.setCurrentActivityId(activityId);
				}
			}
			
			private function onUpdateViewTimeLine(event:SessionEvent):void
			{
				var currentModule:TutoratModule = Model.getInstance().getCurrentTutoratModule() as TutoratModule;
				if(this == currentModule)
				{
					var traceLineObj:Object = Model.getInstance().getTraceLineByUserId(event.userId);
					if(traceLineObj != null)
					{
						var show:Boolean = traceLineObj.show;
						if(!show)
						{
							this.addTraceLineOnViewTraceLineGroup(traceLineObj);
						}					
					}else
					{
						Alert.show("Hasn't time for add new traceline to the model","Bug...");
					}
				}
			}
			
			private function onShowViewTimeLine(event:SessionEvent):void
			{
				var currentModule:TutoratModule = Model.getInstance().getCurrentTutoratModule() as TutoratModule;
				if(this == currentModule)
				{
					
					this.setObsels();
					
					var listTraceLines:ArrayCollection = Model.getInstance().getListTraceLines();
					if(listTraceLines != null)
					{
						var nbrTraceLines:int = listTraceLines.length;
						for(var nTraceLine:int =0; nTraceLine < nbrTraceLines ; nTraceLine++)
						{
							var traceLineObj:Object = listTraceLines[nTraceLine] as Object;
							var show:Boolean = traceLineObj.show;
							if(!show)
							{
								this.addTraceLineOnViewTraceLineGroup(traceLineObj);
							}
						}
					}
				}
			}
			
			private function onCreationCompletTraceLine(event:FlexEvent):void
			{
				// add listener
				this.panelChatSession.addEventListener(FlexEvent.UPDATE_COMPLETE, onUpdateChatSession);
				
				var traceLine:TraceLineB = event.currentTarget as TraceLineB;
				traceLine.removeEventListener(FlexEvent.CREATION_COMPLETE, onCreationCompletTraceLine);
				// add listener edit obsel
				traceLine.addEventListener(ObselEvent.EDIT_OBSEL, onEditObsel);				
				traceLine.listTitleObsels = traceLine.tempList;		
				var userId:int = traceLine.idUserTraceLine;
				traceLine.checkBoxMarkerObsel.selected = true;
				if(userId == Model.getInstance().getLoggedUser().id_user)
				{
					sessionTimeLine.sessionTimeLineLoggedUser.left = traceLine.traceLoggedUser.left;
					this.testCursor = new ObselLine();
					this.testCursor.height = 50;
					var beginTime:Number = new Date().time - this.currentSession.date_start_recording.time;
					this.testCursor.setBegin(beginTime);
					this.testCursor.setEnd(beginTime);
					this.sessionTimeLine.sessionTimeLineLoggedUser.addElement(this.testCursor); 
					this.testTimeLine = traceLine;
					this.startTimer();
				}	
				// get viewObsel for adding him on traceLine
				var viewObsel:ObselSessionOut = getViewObselSessionOut(this._listTempViewObselSessionOut , userId);
				var isFirstEnterSession: Boolean = Model.getInstance().isFirstEnterSession(userId);
				if(viewObsel != null && isFirstEnterSession)
				{
					Model.getInstance().setObsel(viewObsel, userId, TraceModel.SESSION_OUT);	
				}
				
				function getViewObselSessionOut(list:ArrayCollection, userId:int):ObselSessionOut
				{
					var nbrViewObsel:int = list.length;
					for(var nViewObsel:int = 0 ; nViewObsel < nbrViewObsel ; nViewObsel++)
					{
						var viewObsel:ObselSessionOut = list.getItemAt(nViewObsel) as ObselSessionOut;
						if(viewObsel.getOwner() == userId)
						{
							list.removeItemAt(nViewObsel);
							return viewObsel;
						}
					}
					return null;
				}
			}
			/**
			 * listener of the obsel type "SetMarqueur"
			 */
			private function onEditObsel(event:ObselEvent):void
			{
				var obselSkin:ObselMarkerSkin = event.target as ObselMarkerSkin;
				var obselMarker:ObselMarker = obselSkin.hostComponent;
				var text:String = obselMarker.text;
				var obsel:Obsel = obselMarker.parentObsel;
				var typeObsel:String = obsel.type;
				var ownerObsel:int = obsel.props[TraceModel.SENDER];
				if(Model.getInstance().getLoggedUser().id_user == ownerObsel)
				{
					var showWindowEditObselMarker:ObselEvent = new ObselEvent(ObselEvent.SHOW_WINDOW_EDIT_OBSEL);
					showWindowEditObselMarker.obsel = obsel;
					showWindowEditObselMarker.textObsel = text;					
					this.dispatchEvent(showWindowEditObselMarker);
				}
			}
			
			private function setObsels():void{
				var listObsels:ArrayCollection = Model.getInstance().getListObsels();
				if (listObsels != null)
				{	
					var nbrObsels:int = listObsels.length;
					for(var nObsel:int = 0 ; nObsel < nbrObsels; nObsel++)
					{
						var obsel:Obsel = listObsels[nObsel] as Obsel;
						var arrayInfoObsel:Array = Model.getInstance().addObsel(obsel);
						if(arrayInfoObsel.length > 0)
						{
							var infoObsel:Object = arrayInfoObsel[0];
							var senderObsel:int = infoObsel.senderId;
							var textObsel:String = infoObsel.textObsel; 
							// source can be class or paths the type String
							var source = infoObsel.source; 
							initChatPanel(senderObsel, textObsel, source);
							// add documents on the panel the student
							if(obsel.type == TraceModel.RECEIVE_DOCUMENT)
							{
								// get id document
								var idDocument:Number = obsel.props[TraceModel.ID_DOCUMENT];
								switch (obsel.props[TraceModel.TYPE_DOCUMENT])
								{
									case ActivityElementType.VIDEO :
										var player:YoutubePlayer = new YoutubePlayer();
										// set id of the sender user 
										player.setSenderId(infoObsel.senderId);
										player.url = obsel.props[TraceModel.URL];
										player.percentWidth = 100;
										player.height = 225;
										player.toolTip = obsel.props[TraceModel.TEXT];
										player.setIdDocument(idDocument);
										player.addEventListener(PlayerEvent.READY, loadComplete);
										if(this.panelMediaSession != null)
										{
											this.panelMediaSession.addElementAt(player, 0);
										}
										break;
									
									case ActivityElementType.IMAGE :
										var image:ImageDocument = new ImageDocument();
										// set id of the sender user
										image.setSenderId(infoObsel.senderId);
										image.load(obsel.props[TraceModel.URL]);
										image.toolTip = obsel.props[TraceModel.TEXT];
										image.percentWidth = 100;	
										image.setIdDocument(idDocument);
										image.addEventListener(Event.ADDED_TO_STAGE, loadComplete);
										if(this.panelMediaSession != null)
										{
											this.panelMediaSession.addElementAt(image, 0);
										}
										break;
								}
							}						
						}						
					}
				}
			}

			private function initChatPanel(ownerObsel:int, textObsel:String, iconSourceClass:*):void
			{
				// exclus the obsel "Start activity"
				if( ownerObsel != 0)
				{
					var traceLine:Object = Model.getInstance().getTraceLineByUserId(ownerObsel); 
					if(traceLine != null)
					{
						var userAvatar:String = traceLine.userAvatar as String;
						var userName:String = traceLine.userName as String;
						var userColor:uint = traceLine.userColor;
						var sharedElementChat:SharedElementChat = new SharedElementChat();
						sharedElementChat.setElementChat(userAvatar , userName, textObsel , iconSourceClass, userColor);
						sharedElementChat.statVciel = Model.getInstance().checkServeurVisuVciel();
						if(this.panelChatSession != null && this == Model.getInstance().getCurrentTutoratModule() as TutoratModule)
						{
							this.panelChatSession.addElement(sharedElementChat);
						}
					}
				}
			}
			
			
			// add element traceLine
			private function onAddTraceLineElement(event:TraceLineEvent):void
			{
				var userId:int = event.userId;
				var traceLine:TraceLine = event.currentTarget as TraceLine;
				var list:ArrayList = Model.getInstance().getListElementsTraceLineByUserId(userId);
				traceLine.elementsTraceLines = list;
			}
			
			// remove element traceLine
			private function onRemoveTraceLineElement(event:TraceLineEvent):void
			{
				Model.getInstance().setUnvisibleElementTraceLineByUser(event.idElement, event.userId);
			}
			
			private function setEnabledGroupSetMarker(value:Boolean):void
			{
				groupSetMarker.enabled = value;
				if(!value)
				{
					textChatMarker.text = fxgt.gettext("Pose de marqueur non autorisée");
				}else
				{
					textChatMarker.text = "";
				}
			}

			protected function durationSessionSlider_valueCommitHandler(event:FlexEvent):void
			{
				var hSlider:HSlider = event.currentTarget as HSlider;
				var duration:int = hSlider.value;
				// TODO gestion time the session
				this.sessionTimeLine.sessionTimeLineLayout.durationSession = duration;
				var nbrTraceLine:int = this.traceLineGroup.numElements;
				for(var nTraceLine:int = 0; nTraceLine < nbrTraceLine; nTraceLine++)
				{
					var traceLine:TraceLineB = this.traceLineGroup.getElementAt(nTraceLine) as TraceLineB;
					traceLine.durationSession = duration;
				}				
			}


			protected function startTimeSessionSlider_valueCommitHandler(event:FlexEvent):void
			{
				var hSlider:HSlider = event.currentTarget as HSlider;
				var startTime:Number = hSlider.value;
				// TODO gestion time the session`
				var startTimeSessionTimeLayout:Number = startTime - this.currentSession.date_start_recording.time;
				this.sessionTimeLine.sessionTimeLineLayout.startTime = startTimeSessionTimeLayout;
				var nbrTraceLine:int = this.traceLineGroup.numElements;
				for(var nTraceLine:int = 0; nTraceLine < nbrTraceLine; nTraceLine++)
				{
					var traceLine:TraceLineB = this.traceLineGroup.getElementAt(nTraceLine) as TraceLineB;
					traceLine.startTimeSession = startTime;
				}
			}

		]]>
	</fx:Script>
	<modules:states>
		<s:State name="MainState"/>
		<s:State name="BlankState"/>
		<s:State name="StudentState"/>
	</modules:states>
	<fx:Declarations>
		<maps:TutoratMap id="tutoratMap" dispatcher="{this}"/>
		<mate:Listener type="{ApplicationMenuEvent.UPDATE_LANGUAGE}"  method="updateLabelsModule"/>
		<mate:Listener type="{VisuActivityEvent.SHOW_LIST_ACTIVITY}"  method="updateView"/>
		<mate:Listener type="{SessionEvent.NEW_USER_JOIN_SESSION}"  method="onNewUserJoinSession"/>
		<mate:Listener type="{SessionEvent.OLD_USER_OUT_SESSION}"  method="onOldUserOutSession"/>
		<mate:Listener type="{SessionEvent.START_RECORDING_SESSION}"  method="onStartRecordingSession"/>
		<mate:Listener type="{SessionEvent.STOP_RECORDING_SESSION}"  method="onStopRecordingSession"/>
		<mate:Listener type="{SessionEvent.CLOSE_SESSION}"  method="onCloseSession"/>
		<mate:Listener type="{SessionSharedEvent.RECEIVE_SHARED_INFO}"  method="onUpdateViewChatPanelSession"/>
		<mate:Listener type="{SessionEvent.LOAD_LIST_OBSEL}"  method="onShowViewTimeLine"/>
		<mate:Listener type="{SessionEvent.UPDATE_LIST_VIEW_TRACELINE}"  method="onUpdateViewTimeLine"/>
		<mate:Listener type="{VisuActivityEvent.UPDATE_ACTIVITY}"  method="onUpdateViewActivity"/>
		
	</fx:Declarations>
	<mx:HDividedBox width="100%" height="100%" includeIn="MainState,StudentState">
		<s:VGroup width="33%" height="100%" minWidth="200">
			<mx:Spacer height="5" />
			<s:HGroup width="100%">
				<s:Button id="buttonCloseSession" includeIn="MainState" width="50%" height="30"
						   click="buttonCloseSession_clickHandler(event)">
					<s:label>{fxgt.gettext("Clôturer la séance")}</s:label>
				</s:Button>
				<s:Button  id="buttonStartRecording" width="50%" includeIn="MainState" height="30" 
						  click="button1_clickHandler(event)">
				</s:Button>
			</s:HGroup>
				<!-- Démarrer la séance -->
			<s:Panel height="100%" width="100%" id="panelPlanSession" resize="panelPlanSession_resizeHandler(event)">
				<s:title>Plan de séance</s:title>
				<s:title.StudentState>Media de la séance</s:title.StudentState>
				<s:Group includeIn="StudentState" width="100%" height="100%">
					<s:Scroller left="1" right="1" top="1" bottom="1"  id="scrollerPanelMediaSession">    
						<s:Group id="panelMediaSession"   width="100%"  height="100%">
							<s:layout>
								<s:VerticalLayout gap="15"/>
							</s:layout>
						</s:Group>
					</s:Scroller>
				</s:Group>
				<sessions:SessionPlanB  id="sessionPlan" height="100%" width="100%" 
									  shareElement="shareActivityElement(event)"
									  startActivity="startActivity(event)"
									   includeIn="MainState"/>
			</s:Panel>
		</s:VGroup>
		 
		<mx:VDividedBox width="77%" height="100%" minHeight="400" minWidth="550">
			<mx:HDividedBox width="100%" height="55%">
				<s:Panel height="100%" width="40%" title="{'Video : '+ Model.getInstance().getLoggedUser().firstname}" minWidth="200" minHeight="200">
					<s:Group height="100%" width="100%">
						<s:layout>
							<s:VerticalLayout gap="3"/>
						</s:layout>
						<controls:VisuVisio id="visio" width="100%" top="0"
											quality="80"
											bandwidth="15000" autoPlay="True"  height="100%" />
						<s:Group width="100%" bottom="3" height="30" id="groupSetMarker">
							<mx:TextArea  id="textChatMarker" keyUp="onSetMyMarker(event)" enabled="true"   height="25" top="2" left="1" right="155">				
							</mx:TextArea>
							<mx:Button icon="{IconEnum.getIconByTypeObsel(TraceModel.SET_MARKER)}" click="onSetMyMarker(event)" right="1" height="25" top="2"  width="150">
									<mx:label>{fxgt.gettext("Poser un marqueur")}</mx:label>
								</mx:Button>
						</s:Group>	
					</s:Group>
				</s:Panel>
				<s:Panel   height="100%" width="60%" minWidth="300">
					<s:title>Chat</s:title>
					<s:Group height="100%" width="100%">
						<s:layout>
							<s:VerticalLayout gap="3"/>
						</s:layout>
						<s:Group  width="100%"  height="100%">
							<s:Scroller left="1" right="1" top="1" bottom="1" id="scrollerPanelChatSession">    
								<s:Group id="panelChatSession"   width="100%"  top="0">
									<s:layout>
										<s:VerticalLayout  gap="5"/>
									</s:layout>
								</s:Group>
							</s:Scroller> 
						</s:Group>
						<s:Group width="100%" bottom="3" height="30">
							<mx:TextArea    id="textChatMessage" keyUp="onSendMessage(event)" enabled="true"   height="25" top="2" left="1" right="185">				
							</mx:TextArea>
							<mx:Button icon="{IconEnum.getIconByTypeObsel(TraceModel.SEND_CHAT_MESSAGE)}" click="onSendMessage(event)" right="1" height="25" top="2"  width="180">
								<mx:label>{fxgt.gettext("Envoyer le message")}</mx:label>
							</mx:Button>
						</s:Group>	
					</s:Group>
				</s:Panel>
			</mx:HDividedBox>
			<s:Panel width="100%" height="45%" id="panelTimeLine">
				<s:title>{fxgt.gettext("Résumé de l'activité")}</s:title>
				<s:Group width="100%" height="100%">
					<s:layout >
						<s:VerticalLayout gap="0"/>
					</s:layout>
					<timeline:TimeLineSession id="sessionTimeLine" width="100%"/>
					<s:Group width="100%" height="100%">
						<s:Scroller left="1" right="1" top="1" bottom="1">
							<s:Group width="100%" height="100%">
								<s:layout>
									<s:VerticalLayout gap="5"/>
								</s:layout>
								
								<s:Group id="traceLineGroup" width="100%" height="100%">
									<!--<local:TraceLineMXML id="traceLine" width="100%" visible="false"/>-->
									<s:layout>
										<s:VerticalLayout gap="4"/>
									</s:layout>
								</s:Group>
							</s:Group>
						</s:Scroller>
					</s:Group>
					<s:HGroup>
						<s:Label text="Position: "/>
						<s:HSlider id="startTimeSessionSlider" 
								   liveDragging="true" width="300"  valueCommit="startTimeSessionSlider_valueCommitHandler(event)" visible="true"/>
						<s:Label text="Zoom: " />
						<s:HSlider id="durationSessionSlider" 
								   liveDragging="true" width="300" valueCommit="durationSessionSlider_valueCommitHandler(event)"/>
					</s:HGroup>
				</s:Group>
			</s:Panel>
		</mx:VDividedBox>	
	</mx:HDividedBox>	
	<s:Label includeIn="BlankState" text="Rejoindre la séance simplement par l'accueil" fontSize="22" left="50" top="50" width="474"/>
	<s:Button includeIn="BlankState" x="73" y="142" label="Button"  id="debugButton" click="onClickDebugButton(event)" />
	<s:Panel width="700" height="45%" y="200" x="20" includeIn="BlankState">
		<s:title>{fxgt.gettext("Résumé de l'activité")}</s:title>
		<s:Group top="1" width="100%"  height="100%">
			<s:Scroller left="1" right="1" top="1" bottom="1" id="scrollerPanelTimeLine_test">    
				<s:Group id="panelTimeLine_test"   width="100%"  height="100%">
					<s:layout>
						<timeline:TimeSessionLayout/>
					</s:layout>							
				</s:Group>
			</s:Scroller> 
		</s:Group>
	</s:Panel>
</modules:VisuModuleBase>
